@ECHO OFF

CD /D %~dp0

REM SET TO INVALID DRIVE
SET BAK_SRC_DRV=b

SET BAKMIR=bak_mir

(FOR %%C IN (%ALL_DRIVES_NOC%) DO (
	ECHO CHECKING %%C...
	IF EXIST %%C:\meta\bak_mir.main_source.dat (
		SET BAK_SRC_DRV=%%C
		ECHO FOUND MAIN BACKUP SOURCE %%C BREAKING LOOP..
		GOTO :PROCEED
	)
))

ECHO NO BACKUP TARGET CONNECTED. EXITING..
PAUSE
EXIT
GOTO :EOF

:PROCEED
ECHO FOUND MAIN BACKUP SOURCE %BAK_SRC_DRV%
REM PAUSE

IF NOT EXIST %BAK_SRC_DRV%:\%BAKMIR% (
	ECHO %BAK_SRC_DRV%:\%BAKMIR% NOT AVAILABLE, EXITING..
	PAUSE
	GOTO :EOF
)

(FOR %%C IN (%ALL_DRIVES_NOC%) DO (
	CALL :DOMIRR %%C
))

GOTO :EOF

:DOMIRR
	SET TGTDRV=%~1

	IF NOT EXIST %TGTDRV%:\meta\bak_mir.%BAKMIR%.dat (
		ECHO NOT A BACKUP MIRROR TARGET %TGTDRV% SKIPPING..
		GOTO :EOF
	)

	IF NOT EXIST %TGTDRV%:\%BAKMIR% (
		MKDIR %TGTDRV%:\%BAKMIR%
	)

	ECHO MIRRORING FROM %BAK_SRC_DRV% TO %TGTDRV%
	REM PAUSE

	REM BACKUP ONLY DIRS LISTED IN LST FILE
	SET MIR_LST=%TGTDRV%:\meta\bak_mir.%BAKMIR%.lst

	IF EXIST %MIR_LST% (

		FOR /F "tokens=1,2 delims=;" %%F IN (%MIR_LST%) DO (
			TITLE MIRRORING FROM [%BAK_SRC_DRV%:\%BAKMIR%\%%F] TO [%TGTDRV%:\%BAKMIR%\%%F] WITH OPTS [%%G]

			IF EXIST "%BAK_SRC_DRV%:\%BAKMIR%\%%F" (
				ROBOCOPY "%BAK_SRC_DRV%:\%BAKMIR%\%%F" "%TGTDRV%:\%BAKMIR%\%%F" /MIR /J /XD ".tresorit" ".tmp.drivedownload" %%G
			)
		)

	)

	REM BACKUP ALL BAK_MIR DIR
	IF NOT EXIST %MIR_LST% (

		IF EXIST %BAK_SRC_DRV%:\%BAKMIR% (
		IF EXIST %TGTDRV%:\%BAKMIR% (
			TITLE MIRRORING FROM %BAK_SRC_DRV% TO %TGTDRV%
			ROBOCOPY %BAK_SRC_DRV%:\%BAKMIR% %TGTDRV%:\%BAKMIR% /MIR /XD ".tresorit" ".tmp.drivedownload" /J
		))

	)

GOTO :EOF
